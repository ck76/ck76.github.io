[TOC]

### config.gradle

```groovy
ext {

    android = [
            compileSdkVersion: 27,
            buildToolsVersion: "23.0.2",
            applicationId    : "cn.ck.xjbl",
            minSdkVersion    : 18,
            targetSdkVersion : 27,
            versionCode      : 1,
            versionName      : "1.0"
    ]

    dependVersion = [
            supportVersion  : "27.1.1",
            greendao        : "3.2.2",
            greendaoUpgrade : "v2.0.1",
            butterknife     : "8.8.1",
            leakcanary      : "1.5.4",
            rxJava          : "2.1.13",
            rxAndroid       : "2.0.2",
            retrofit        : "2.4.0",
            okHttp          : "3.10.0",
            gson            : "2.8.5",
            rxlifecycle     : "2.2.1",
            glide           : "4.8.0",
            photoView       : "2.1.3"
    ]

    supportLib = [
            v7              : "com.android.support:appcompat-v7:$dependVersion.supportVersion",
            design          : "com.android.support:design:$dependVersion.supportVersion",
            recyclerview    : "com.android.support:recyclerview-v7:$dependVersion.supportVersion"
    ]

    butterknife = [
            butterknife     : "com.jakewharton:butterknife:$dependVersion.butterknife",
            butterknife_apt : "com.jakewharton:butterknife-compiler:$dependVersion.butterknife"
    ]

    leakcanary = [
            leakcanary      : "com.squareup.leakcanary:leakcanary-android:$dependVersion.leakcanary",
            leakcanary_nonp : "com.squareup.leakcanary:leakcanary-android-no-op:$dependVersion.leakcanary"
    ]

    greendao = [
            greendao        : "org.greenrobot:greendao:$dependVersion.greendao",
            greendao_upgrade: "com.github.yuweiguocn:GreenDaoUpgradeHelper:$dependVersion.greendaoUpgrade"
    ]

    rx = [
            rxJava          : "io.reactivex.rxjava2:rxjava:$dependVersion.rxJava",
            rxAndroid       : "io.reactivex.rxjava2:rxandroid:$dependVersion.rxAndroid",
            rxlifecycle     : "com.trello.rxlifecycle2:rxlifecycle:$dependVersion.rxlifecycle",
            rxlife_android  : "com.trello.rxlifecycle2:rxlifecycle-android:$dependVersion.rxlifecycle",
            rxlife_component: "com.trello.rxlifecycle2:rxlifecycle-components:$dependVersion.rxlifecycle"
    ]

    retrofit = [
            retrofit        : "com.squareup.retrofit2:retrofit:$dependVersion.retrofit",
            retrofit_gson   : "com.squareup.retrofit2:converter-gson:$dependVersion.retrofit",
            retrofit_rxjava : "com.squareup.retrofit2:adapter-rxjava2:$dependVersion.retrofit"
    ]

    okHttp = [
            okHttp          : "com.squareup.okhttp3:okhttp:$dependVersion.okHttp",
            okHttp_logging  : "com.squareup.okhttp3:logging-interceptor:$dependVersion.okHttp"
    ]

    image = [
            glide           : "com.github.bumptech.glide:glide:$dependVersion.glide",
            glide_okhttp    : "com.github.bumptech.glide:okhttp3-integration:$dependVersion.glide",
            photoView       : "com.github.chrisbanes:PhotoView:$dependVersion.photoView"
    ]

    gson                    = "com.google.code.gson:gson:$dependVersion.gson"

}
```



### 然后再项目根目录build.gradle下引用

```
apply from: "config.gradle"
```



### 在模块下引用

```groovy
apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply plugin

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            buildConfigField("boolean", "LOG_DEBUG", "true")
            buildConfigField("String", "API_BASE_URL", "\"https://raw.githubusercontent.com/ck76/API/master/XJBL/\"")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

        }
    }
    productFlavors {
    }

    greendao {
        schemaVersion 1
        daoPackage "cn.ck.xjbl.database.greendao.dao"
        targetGenDir "src/main/java"
        generateTests false
        targetGenDirTests "src/androidTest/java"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.supportLib.v7
    implementation rootProject.ext.supportLib.design
    implementation rootProject.ext.supportLib.recyclerview
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:multidex:1.0.3'

    implementation rootProject.ext.butterknife.butterknife
    annotationProcessor rootProject.ext.butterknife.butterknife_apt
    debugImplementation rootProject.ext.leakcanary.leakcanary
    releaseImplementation rootProject.ext.leakcanary.leakcanary_nonp

    implementation rootProject.ext.gson
    implementation rootProject.ext.retrofit.retrofit
    implementation rootProject.ext.retrofit.retrofit_gson
    implementation rootProject.ext.retrofit.retrofit_rxjava
    implementation rootProject.ext.okHttp.okHttp
    implementation rootProject.ext.okHttp.okHttp_logging

    implementation rootProject.ext.rx.rxJava
    implementation rootProject.ext.rx.rxAndroid
    implementation rootProject.ext.rx.rxlifecycle
    implementation rootProject.ext.rx.rxlife_android
    implementation rootProject.ext.rx.rxlife_component

    implementation rootProject.ext.greendao.greendao
    implementation rootProject.ext.greendao.greendao_upgrade

    implementation rootProject.ext.image.glide
    implementation rootProject.ext.image.glide_okhttp
    implementation rootProject.ext.image.photoView

}
```

